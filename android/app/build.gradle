apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "com.facebook.react"

//
// SDK-level codes / values cheat sheet:
//
// Level    Codename            Android OS Ver
// 18       JELLY_BEAN_MR2      4.3
// 19       KITKAT              4.4
// 20       KITKAT_WATCH        4.4W
// 21       LOLLIPOP            5.0
// 22       LOLLIPOP_MR1        5.1
// 23       M                   6.0
// 24       N                   7.0
// 25       N_MR1               7.1, 7.1.1
// 26       O                   8.0
// 27       O_MR1               8.1
// 28       P                   9
//

// Check if we're running in a CI environment
def isCiBuild = System.getenv("CI") != null && System.getenv("CI").toBoolean()
// Alternatively, you can also check for GitHub Actions specifically
def isGitHubActions = System.getenv("GITHUB_ACTIONS") != null && System.getenv("GITHUB_ACTIONS").toBoolean()
// Combined flag to determine if we should bundle JS
def shouldBundleJS = isCiBuild || isGitHubActions

// Task to generate React Native bundle for CI builds
if (shouldBundleJS) {
    tasks.register("createJSBundle", Exec) {
        workingDir "../../"
        commandLine "yarn", "react-native", "bundle", "--platform", "android", "--dev", "false", 
                    "--entry-file", "index.tsx", 
                    "--bundle-output", "android/app/src/main/assets/index.android.bundle", 
                    "--assets-dest", "android/app/src/main/res/"
        doFirst {
            new File("${projectDir}/src/main/assets").mkdirs()
        }
    }
}

android {
  compileSdkVersion 34

  // compileKotlin {
  //     kotlinOptions.allWarningsAsErrors = false
  // }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_17.toString()
  }
  defaultConfig {
    applicationId "com.github.quarck.calnotify"
    minSdkVersion 24
    minSdk 24
    //noinspection ExpiredTargetSdkVersion
    targetSdkVersion 25
    versionCode 4000018
    versionName "6.7.0"

    buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"

    // We removed abiFilters since it conflicts with splits
    // ndk { 
    //   abiFilters 'x86_64', 'arm64-v8a'
    // }

    // Add packagingOptions to include necessary .so files
    packagingOptions {
      pickFirst '**/*.so'
    }
  }

  // Add splits configuration for APK size optimization
  splits {
    abi {
      enable true
      reset()
      include 'x86_64', 'arm64-v8a'
      universalApk false
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

      lintOptions {
        disable 'MissingTranslation'
        abortOnError false
      }
    }

    customDebugType {
      initWith debug
      matchingFallbacks = ['debug']
      debuggable true
    }
  }

  sourceSets {
    main {
      java.srcDirs += 'src/main/kotlin'
      // Add JNI libs directory
      jniLibs.srcDirs = ['src/main/jniLibs']
    }
  }
  namespace 'com.github.quarck.calnotify'

}

// Ensure the CI build generates the bundle before assembling
if (shouldBundleJS) {
    tasks.whenTaskAdded { task ->
        if (task.name.contains('assemble') || task.name.contains('bundle')) {
            task.dependsOn createJSBundle
        }
    }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  // Kotlin
  //implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

  // AndroidX
  // https://mvnrepository.com/artifact/androidx.legacy/legacy-support-v4
  implementation "androidx.legacy:legacy-support-v4:1.0.0"

  implementation "androidx.core:core:$androidx_lib_version"

  implementation "androidx.core:core-ktx:$androidx_lib_version"

  implementation "androidx.appcompat:appcompat:$androidx_lib_version"

  // https://mvnrepository.com/artifact/androidx.recyclerview/recyclerview
  implementation "androidx.recyclerview:recyclerview:1.3.2"

  // https://mvnrepository.com/artifact/androidx.cardview/cardview
  implementation "androidx.cardview:cardview:1.0.0"

  // https://mvnrepository.com/artifact/androidx.palette/palette
  implementation "androidx.palette:palette:1.0.0"

  // Material
  // https://mvnrepository.com/artifact/com.google.android.material/material
  implementation "com.google.android.material:material:1.12.0"
  
  // React Native
  implementation "com.facebook.react:react-android"
  implementation "com.facebook.react:hermes-android"

  implementation 'com.github.requery:sqlite-android:3.45.0'
}

// https://reactnative.dev/docs/react-native-gradle-plugin
react {
  entryFile = file("../../index.tsx")
  // Only enable bundling on CI builds to avoid bundling during local development
  // bundleInDebug = shouldBundleJS
  // bundleInRelease = true
}


apply from: file("../../node_modules/@react-native-community/cli-platform-android/native_modules.gradle"); applyNativeModulesAppBuildGradle(project)
