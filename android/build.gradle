// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {
      kotlin_version = '1.9.25'
      kotlinVersion = '1.9.25'

      // App versions (from master - known working for builds)
      androidx_lib_version = '1.6.0'
      androidxLibVersion = '1.6.0'
      android_core_version = '1.6.0'
      androidCoreVersion = '1.6.0'

      // Test versions (from feature branch - known working for tests)
      androidx_test_version = '1.3.1'
      android_core_test_version = '1.3.1'

      gradleVersion = '8.6.0'
      gradle_version = '8.6.0'

      // WARNING the android gradle plugin VERIONS != TO GRADLE PLUGIN VERSION
      // https://developer.android.com/build/releases/gradle-plugin

      androidGradlePluginVersion = '8.4.0'
      android_gradle_plugin_version = '8.4.0'

      // WARNING: WE BUILT CR-SQLITE WITH THIS VERSION OF NDK
      // IF YOU CHANGE THIS, YOU NEED TO REBUILD CR-SQLITE
      ndkVersion = '26.1.10909125'
      minSdk = 24 // https://github.com/software-mansion/react-native-screens/pull/2251#ref-issue-2457327086
      minSdkVersion = 24
      
      // Supported architectures
      supportedArchs = ['arm64-v8a', 'x86_64']
    }

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$android_gradle_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath("com.facebook.react:react-native-gradle-plugin")

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

subprojects {
    configurations.all {
        if (name.contains('AndroidTest')) {
            resolutionStrategy {
                // Force test dependencies to use specific versions
                force "androidx.appcompat:appcompat:$androidx_test_version"
                force "androidx.appcompat:appcompat-resources:$androidx_test_version"
                force "androidx.core:core:$android_core_test_version"
                force "androidx.core:core-ktx:$android_core_test_version"
                force "androidx.fragment:fragment:$androidx_test_version"
                force "androidx.lifecycle:lifecycle-runtime:2.5.1"
                force "androidx.lifecycle:lifecycle-viewmodel:2.5.1"
                force "androidx.annotation:annotation:1.4.0"
                force "androidx.vectordrawable:vectordrawable:1.1.0"
                force "androidx.vectordrawable:vectordrawable-animated:1.1.0"
                force "androidx.activity:activity:1.4.0"
                force "androidx.activity:activity-ktx:1.4.0"
                force "com.google.android.material:material:1.6.1"
            }
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
