// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {
      kotlin_version = '2.0.21'
      kotlinVersion = '2.0.21'

      // App versions (from master - known working for builds)
      androidx_lib_version = '1.6.0'
      androidxLibVersion = '1.6.0'
      android_core_version = '1.6.0'
      androidCoreVersion = '1.6.0'

      gradleVersion = '8.13'
      gradle_version = '8.13'

      // WARNING the android gradle plugin VERIONS != TO GRADLE PLUGIN VERSION
      // https://developer.android.com/build/releases/gradle-plugin

      // Using 8.7.3 for JaCoCo compatibility with Gradle 8.13
      androidGradlePluginVersion = '8.13.2'
      android_gradle_plugin_version = '8.13.2'

      // WARNING: WE BUILT CR-SQLITE WITH NDK 26.1.10909125
      // React Native 0.81 requires NDK 27+ for std::format support
      // CR-SQLITE may need to be rebuilt if issues arise
      ndkVersion = '27.2.12479018'
      // SDK versions - aligned with Expo SDK 54 requirements
      minSdk = 24
      minSdkVersion = 24
      compileSdkVersion = 36
      targetSdkVersion = 36
      
      // Supported architectures
      supportedArchs = ['arm64-v8a', 'x86_64']
    }

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$android_gradle_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        // Note: KSP has binary incompatibility with Kotlin 2.0.21, using KAPT instead
        // Note: React Native Gradle Plugin is now loaded via includeBuild in settings.gradle (RN 0.76+)

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
  id("com.facebook.react.rootproject")
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { 
            url "https://jitpack.io"
            content {
                includeGroup "com.github.requery"
            }
        }
        maven { url "https://maven.google.com" }
    }
    
    configurations.all {
        resolutionStrategy {
            // Cache dynamic versions for 10 minutes
            cacheDynamicVersionsFor 10 * 60, 'seconds'
            // Cache changing modules for 10 minutes
            cacheChangingModulesFor 10 * 60, 'seconds'
        }
    }
}

// Apply packagingOptions to all Android library subprojects to handle duplicate native libs
subprojects {
    plugins.withId("com.android.library") {
        android {
            packagingOptions {
                pickFirst '**/libc++_shared.so'
                pickFirst '**/*.so'
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
